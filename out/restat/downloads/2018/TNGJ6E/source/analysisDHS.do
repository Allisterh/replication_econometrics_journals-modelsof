/* analysisDHS 4.00              Bhalotra/Clarke           yyyy-mm-dd:2014-11-03
*---|----1----|----2----|----3----|----4----|----5----|----6----|----7----|----8

This file contains replication materials for results based on the DHS data prese
nted in the paper (Tables 4 and 6).  Full details regarding creating this data f
rom each country's DHS survey -- including downloading and merging the full set
of DHS files, can be found in the README file circulated with this code.

This do file uses as input the data file DHS_twins_IMR, which is generated by the
file Twin_Setup.do. Currently it includes all DHS files available up until Octob-
er 2013. To replicate results exactly no DHS files released after 2013 should be
included.

This file requires a the user written ado estout which can be installed from the
SSC. If this is not installed on the computer/server, it will be installed autom
atically. If it is not installed and the computer does not have internet access,
this file will fail when trying to export results.

For optimal viewing set tab width equal to 4 in text editor.

Questions should be directed to damian.clarke@usach.cl

*/


clear all
version 11
set more off
cap log close
set seed 2727

cap which estout
if _rc!=0 cap ssc install estout

*******************************************************************************
*** (0) Globals and Locals
*******************************************************************************
global DAT "./../data"
global OUT "./../results"
global LOG "./../log"

log using "$LOG/analysisDHS.txt", text replace


* VARIABLES
global age motherage motheragesq motheragecub agefirstbirth 
global S _educf* _wealth*
global H height bmi preClustD preClustN preClustZ

* ECONOMETRIC SPECIFICATIONS
local se   cluster(id)
local wt   [pw=sweight]
local cond if age<19



********************************************************************************
**** (1) Estimate Table 4 panel A using Infant Mortality File
********************************************************************************
use "$DAT/DHS_twins_IMR", clear
tab wealth, gen(_wealth)
tab educf , gen(_educf)

gen tta=age if twind==1
bys id: egen twinage=min(tta)
gen twinagedif=twinage-age

gen treated=twinfamily>=1
replace treated=. if twinfamily>=1&twinagedif>-1
**MUST BE CHILDREN BORN AT LEAST 1 YEAR PRIOR TO TWINS (fully exposed to IMR)

lab var treated "Treated"

egen ids = group(id)
xtset ids

replace infantmortality=infantmortality*100
local bvar $age i._cou i.year_birth twind

sum infantmortality [aw=sweight] if two_plus==1
local itwo = r(mean)
sum infantmortality [aw=sweight] if three_plus==1
local ithree = r(mean)
sum infantmortality [aw=sweight] if four_plus==1
local ifour = r(mean)

foreach n in two three four {
    local c  if age>=1&age<=18&`n'_plus==1
    preserve
    keep `c'
    collapse treated infantmort $age _cou year_birth twind $S $H sweight, by(id)

    reg infantmortality treated `bvar' $S $H `wt', `se' 
    keep if e(sample)==1
    eststo: reg infantmortality  treated $age `wt', `se' 
    estadd scalar mval = `i`n''
    restore
}

#delimit ;
esttab est1 est2 est3 using "$OUT/main/IMRTest_DHS_mother.tex",
keep(treated) b(%-9.3f) se(%-9.3f) starlevel ("*" 0.10 "**" 0.05 "***" 0.01) 
stats(mval N, fmt(%9.3f %9.0gc) label("Mean Value" "Observations"))
label nonotes mlabels(, none) nonumbers style(tex) fragment replace noline;
#delimit cr
estimates clear

*******************************************************************************
*** (2) Estimate Table 6: Selection into twinning based on healthy survival
*******************************************************************************
keep if _merge == 3
bys id: gen mothercount=_n
keep if mothercount==1
merge 1:1 id using "$DAT/TwinsMMR", gen(_mergeMM)

gen anyMMR=numMaternalDeaths>=1&numMaternalDeaths<=5 if numMaternalDeaths!=.
gen deathsperSister=numMaternalDeaths/numSisters
gen SMA2=SiblingMeanAge^2
gen SMA3=SiblingMeanAge^3
gen heightLess140=height<140
    
qui reg anyMMR $age malec i._cou i.year_birth i.age i.contracep_intent
predict concentratedMMR if e(sample), residuals
    
foreach num of numlist 1(1)4 {
    gen unhealthy`num' = height<(135+5*`num')|bmi<(15.5+0.5*`num')
    gen healthy`num'  = (unhealthy`num'-1)*-1
}

****************************************************************************
*** (2a) Alderman-style bounds (Table 6)
****************************************************************************
local tvars motherage motheragesq agefirstbirth educf educfyrs_sq height bmi 
local FEs   i.child_yob i._cou 

tab educf
gen twinfam100=100 if twinfamily==1|twinfamily==2
replace twinfam100=0 if twinfamily==0

eststo: reg twinfam100 `tvars' `FEs' `wt' `cond'
 
foreach num of numlist 1(1)4 {
    sum deathsperSister if healthy`num'==1
    local healthexp   =round(`r(mean)'*`r(N)')
    dis `healthexp'
    sum deathsperSister if healthy`num'==0
    local unhealthexp =round(`r(mean)'*`r(N)')
    dis `unhealthexp'
    
    set seed 2203
    gen rnumb = runiform()
    sort healthy`num' rnumb
    bys healthy`num': gen counter=_n
    gen     repeat=1 if counter<=`healthexp'&healthy`num'==1
    replace repeat=1 if counter<=`unhealthexp'&healthy`num'==0

    expand 2 if repeat==1, gen(created)
    replace twinfam100 = 100 if created==1&healthy`num'==0
    replace twinfam100 = 0   if created==1&healthy`num'==1
    eststo: reg twinfam100 `tvars' `FEs' `wt' `cond'
 
    drop if created==1
    drop rnumb repeat counter created
}

lab var height "Height"
lab var bmi    "BMI"
#delimit ;
esttab est1 est2 est3 est4 est5 using "$OUT/main/Alderman.tex", b(%-9.3f) 
keep(height bmi) se(%-9.3f) starlevel ("*" 0.10 "**" 0.05 "***" 0.01) 
stats(N r2, fmt(%9.0gc %5.3f) label("Observations" "R-Squared"))
label nonotes mlabels(, none) nonumbers style(tex) fragment replace noline;
#delimit cr
estimates clear

****************************************************************************
*** (2b) Graph of sister height and MMR (Figure A1)
****************************************************************************
gen cut=.
local it=1
foreach cut of numlist 140 145 150 155 160 165 170 175 {
    replace cut=`it' if height<`cut'&cut==.
    local ++it
}
label def ht 1 "<140" 2 "145" 3 "150" 4 "155" 5 "160" 6 "165" 7 "170" 8 "175+"
label values cut ht
 
sum height, d
local mid = r(p50)
local psd = r(p50)+r(sd)
local msd = r(p50)-r(sd)
dis "`mid'"
 
 
preserve
collapse concentratedMMR (semean) se_MMR=concentratedMMR, by(cut)
gen ub = concentratedMMR+1.96*se_MMR
gen lb = concentratedMMR-1.96*se_MMR
#delimit ;    
twoway rcap ub lb cut, xlabel(1 2 3 4 5 6 7 8, valuelabel) scheme(s1mono) ||  
scatter concentratedMMR cut, xtitle("Height (woman)")
    ytitle("Maternal Mortality (sisters)") xline(4.1, lpattern(solid))
    mcolor(black) msymbol(o) legend(order(2 "Maternal Mortality" 1 "95% CI"));
graph export "$OUT/appendix/MMRcuts.eps", as(eps) replace;
#delimit cr
restore


********************************************************************************
**** (3) Twin predict regressions (Table A8)
********************************************************************************
use "$DAT/DHS_twins_IMR", clear
keep if _merge == 3
keep if age<19
tab wealth, gen(_wealth)
tab educf , gen(_educf)

egen keeper = rowmiss(_cou year_bi age contracep_i $age $H educf fert)
keep if keeper == 0

sum two_plus three_plus four_plus
gen n234 = two_plus==0&three_plus==0&four_plus==0
sum n234

local tvars motherage motheragesq agefirstbirth educf height bmi 
local FEs   i.child_yob i._cou 
fvset base 1 _cou
fvset base 1 child_yob


eststo: reg twind100 `tvars' `FEs' `wt', `se'
foreach inc in =="L" !="L" {
    eststo: reg twind100 `tvars' `FEs' `wt' `cond'&inc_status`inc', `se'
}

local cond1 child_yob>1990
local cond2 child_yob<=1990
foreach condtn in cond1 cond2 {
    eststo: reg twind100 `tvars' `FEs' `wt' if ``condtn'', `se'
}

bys v001 _year: egen CprenateDoctor = mean(prenate_doc)
bys v001 _year: egen CprenateNurse  = mean(prenate_nurse)
bys v001 _year: egen CprenateNone   = mean(prenate_none)

eststo: reg twind100 `tvars' `FEs' Cprenate* `wt', `se'

lab var motherage      "Mother's Age"
lab var motheragesq    "Mother's Age Squared"
lab var agefirstbirth  "Age at First Birth"
lab var educf          "Mother's Education (years)"
lab var educfyrs_sq    "Mother's Education Squared"
lab var height         "Mother's Height (cm)"
lab var bmi            "Mother's BMI"
lab var CprenateDoc    "Prenatal Care (Doctor)"
lab var CprenateNurse  "Prenatal Care (Nurse)"
lab var CprenateNone   "Prenatal Care (None)"

#delimit ;
esttab est1 est2 est3 est4 est5 est6 using "$OUT/appendix/twinsDHS.tex",
b(%-9.3f) se(%-9.3f) keep(`tvars' CprenateDoctor CprenateNurse CprenateNone)
starlevel ("*" 0.10 "**" 0.05 "***" 0.01) nogaps
stats(N r2, fmt(%12.0gc %-5.3f) label(Observations R-Squared))
label nonotes mlabels(, none) nonumbers style(tex) fragment replace noline;
#delimit cr
estimates clear
    

********************************************************************************
**** (4) Clean up
********************************************************************************
log close
