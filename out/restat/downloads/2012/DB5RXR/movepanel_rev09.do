version 8
clear
set memory 500m
capture log close
set more off

*****************************************************
* Make panel movepanel.dta . Contains info of all 
* individuals with match in industripanel (not only full time workers)
* and info of these individuals the year before or after they are 
* observed in a manufacturing plant.
***********************************************************************
* Prepare panel 
	use ${pap4data}matchpanel.dta
	drop if pid==.
	sort pid aar
	count
	count if bnr==.
	keep aar pid bnr
	sort pid aar
	save ${pap4data}movepanel.dta, replace
* this data set now contains only pid and bnr when pid is found in manufacturing
* Contains pid with missing bnr when pid is found in manufacturing plant the year
* before or after (generated by fillin command - thus some of these pid year
* combinations will not exist in regdir)


* Merge in info about where/if pid works the years when pid is not
* observed in manufacturing plant (earnings, hours, plant number)
* This info comes from regdir
	forvalues t=1989/2000 {
		use ${REG_DIR}adssb`t', clear
		rename yr aar
		keep aar plant pid pearn hrs status eduy edut89 edut00 isic5
		sort pid aar
		merge pid aar using ${pap4data}movepanel.dta
		quietly keep if _merge==3
		drop _merge
		compress
		quietly save ${pap4data}reg`t', replace 
	}
	use ${pap4data}reg1989.dta, clear
	forvalues t=1990/2000 {
		append using ${pap4data}reg`t'.dta
	}
	sort pid aar
	merge pid aar using ${pap4data}movepanel.dta
	* merge=2 for filled in pid year combinations in
	* matchpanel that do not exist in regdir
	assert _merge!=1
	quietly keep if _merge==3
	drop _merge
	forvalues t=1989/2000 {
		erase ${pap4data}reg`t'.dta
	}
	compress
*Merge in MNE indicators
	sort bnr aar
	quietly save ${pap4data}movepanel, replace
	merge bnr aar using ${industri}mne.dta
	tab _merge
	* Merge=2 in 1989 and for the obs where the individual
	* does not work in manufacturing (ie. bnr==.)
	quietly drop if _merge==2
	drop _merge

* Merge in totutenl from foumerge
	sort bnr aar
	merge bnr aar using ${industri}foumerge_mob, keep(totutenl)
	tab _merge
	quietly drop if _merge==2 
	drop _merge
* Generate foreign ownership dummy
	quietly gen FD=1 if totutenl==1
	quietly replace FD=1 if totutenl==2
	quietly replace FD=0 if totutenl==0 
	count if FD==. & bnr!=.
* Generate MNE dummy
	quietly gen MNE=1 if formne==1
	quietly replace MNE=2 if dommne==1
	replace MNE=0 if MNE==. & bnr!=.
	quietly save ${pap4data}movepanel.dta, replace

capture log close
exit
