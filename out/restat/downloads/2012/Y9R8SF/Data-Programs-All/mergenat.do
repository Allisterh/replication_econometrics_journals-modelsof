*************************************************************************
* Almond, Hoynes, and Schanzenbach, 					*
* "Inside the War on Poverty: The Impact of the Food Stamp Program on Birth Outcomes" *
*Review of Economics and Statistics, May 2011, Vol. 93, No. 2: 387-403. * 
*************************************************************************

***************************
*mergenat.do
*merges natality data together with foodstamps and cwalk; collapses data into cells
***************************


/********************************************************************************************
* USING FENTON 2003 10TH PERCENTILE BABY WEIGHT THRESHOLDS BY GESTATION, 
	CALUCULATE THE SHARE OF BIRTHS BELOW THIS THRESHOLD.  
********************************************************************************************/

#delimit ;

program define addFenton ;
	local weeks 	"22 23 24 25 26 27 28 29 30 31 32 33 34 35 36  
			37 38 39 40 41 42 43 44 45 46 47 48 49 50" ;
	local fentonThres "384.2395354 449.6508548 512.6903547 574.9594163 638.9963309 711.6076946 
			797.6180189 900.758318 1022.761453 1170.488991 1345.674452 1544.210478 
			1758.088963 1983.949777 2214.862238 2388.92344 2563.110487 2737.499501  
			2912.038504 3056.670053 3204.840915 3353.854733 3504.866505 3663.809379 
			3805.84609 3966.881744 4125.630093 4282.283863 4408.728515" ;
	gen fenton10th = .;
	label variable fenton10th "Share of births below Fenton 2003 10perc weight threshold";
	foreach j of numlist 1(1)29 {;
		local w : word `j' of `weeks';
		local t : word `j' of `fentonThres';
		replace fenton10th = 0 if gest == `w' & bweight > `t';
		replace fenton10th = 1 if gest == `w' & bweight <= `t';
		macro drop w t;
		};
	gen fentonMiss = (fenton10th == .);
	macro drop weeks fentonThres ;
	end;
#delimit cr

*******************************
* BEGIN MERGE NATALITY 
*******************************

local dataset = 1

while `dataset'<=1 {

  drop _all
  capture log close
  set memory 800m
  set more off

  tempfile tffs tfcw tf1 tf2 tf3 tf4 tf5 tf6 tf7 tf


  *************
  *foodstamp county start date
  *************

  use foodstamps, clear
  keep stfips countyfips fs_month fs_year 
  sort stfips countyfips

  save `tffs', replace

  ***********************************************************************
  * Define the datasets							*
  * Each dataset is collapsed by different variables.  This dofile 	*
  * (mergenat.do) will loop through and make each dataset.		*
  ***********************************************************************

  *DATASET 1
  local collapse1="(sum) nbirths nbirths_gest (mean) bw* attend* gest* sex twin multiple congenital fenton10th fentonMiss";
  local collapseby1="mom_race stfips countyfips year month"
  local name1="main"

  *DATASET 2
  local collapse2="(sum) nbirths (mean) bw* attend* gest* sex"
  local collapseby2="secondkid mom_race stfips countyfips year month"
  local name2="parity"

  *DATASET 3
  local collapse3="(sum) nbirths (mean) bweight bw2500 bw1500 gest37 sex"
  local collapseby3="mom_agegroup mom_race stfips countyfips year month"
  local name3="age"

  *DATASET 4
  local collapse4="(sum) nbirths (mean) bweight bw2500 bw1500 gest37 sex"
  local collapseby4="mom_edgroup mom_race stfips countyfips year month"
  local name4="educ"

  *DATASET 5
  local collapse5="(sum) nbirths"
  local collapseby5="mom_agedet mom_race stfips countyfips year month"
  local name5="agedet"

  *DATASET 6
  local collapse6="(sum) nbirths (mean) bweight bw2500 bw1500 gest37 sex"
  local collapseby6="havefather mom_race stfips countyfips year month"
  local name6="havefather"

 *DATASET 7
  local collapse7="(sum) nbirths (mean) bw* attend* gest*"
  local collapseby7="mom_race sex stfips countyfips year month"
  local name7="sex"


  ************
  *1968 Data *
  ************

  forvalues i=68(1)68 {

    capture label drop _all
    drop _all

* crosswalk for facilitating county matching

    qui use nat_cwalk, clear

    keep stfips countyfips nstate ncnty`i' ncity`i'
    drop if ncnty`i'==-99
    duplicates drop
    isid nstate ncnty`i' ncity`i'

    sort nstate ncnty`i' ncity`i'

    capture drop _merge
    save `tfcw', replace

* generated by makenat.do

    qui nattest19`i', clear

    *rename state and county to match cwalk var names
    rename res_state nstate
    rename res_county ncnty`i'
    rename res_city ncity`i'

    *create flag for missing gestation data
    gen gest_miss=(gest==.)
    
    *modify nbirths for gestation missing
    gen nbirths_gest=nbirths
    replace nbirths_gest=0 if gest==.

    *drop counties w/out county vitals code (-99)
    drop if ncnty`i'==-99

    * ADD FENTON THRESHOLD INDICATOR 
    addFenton

    sort nstate ncnty`i' ncity`i'

    capture drop _merge
    merge nstate ncnty`i' ncity`i' using `tfcw'
    tab nbirths if _merge!=3, missing
    drop if _merge!=3
    drop _merge

    *set missing countyfips (-99) to "."
    replace countyfips=. if countyfips==-99

    *collapse here!
    collapse `collapse`dataset'' [pweight=recordweight], by(`collapseby`dataset'' `demographiccut`dataset'')

    capture append using `tf`dataset''
    save `tf`dataset'', replace

  }

  ************
  *1969-77
  ************

  forvalues i=69(1)77 {

    capture label drop _all
    drop _all

    qui use nat_cwalk, clear

    keep stfips countyfips nstate ncnty`i' ncity`i'
    drop if ncnty`i'==-99
    duplicates drop
    isid nstate ncnty`i' ncity`i'

    sort nstate ncnty`i' ncity`i'

    capture drop _merge
    save `tfcw', replace

* Made in makenat.do

    qui use nattest19`i', clear

    *rename state and county to match cwalk var names
    rename res_state nstate
    rename res_county ncnty`i'
    rename res_city ncity`i'

    *create flag for missing gestation data
    gen gest_miss=(gest==.)
    
    *modify nbirths for gestation missing
    gen nbirths_gest=nbirths
    replace nbirths_gest=0 if gest==.

    *drop counties w/out county vitals code (-99)
    drop if ncnty`i'==-99

    * ADD FENTON THRESHOLD INDICATOR 
    addFenton

    sort nstate ncnty`i' ncity`i'

    capture drop _merge
    merge nstate ncnty`i' ncity`i' using `tfcw'
    tab nbirths if _merge!=3, missing
    drop if _merge!=3
    drop _merge

    *set missing countyfips (-99) to "."
    replace countyfips=. if countyfips==-99

    *don't need this in collapse vars of attend*
    drop attend

    *collapse here!
    collapse `collapse`dataset'' [pweight=recordweight], by(`collapseby`dataset'' `demographiccut`dataset'')

    capture append using `tf`dataset''
    save `tf`dataset'', replace

  }

  *******************
  *merge in foodstamps here
  *******************

  sort stfips countyfips
  merge stfips countyfips using `tffs'
  tab _merge
  tab stfips if _merge!=3
  drop _merge

  *********
  *edits, labels, etc.
  *********

  label var stfips	"State of Residence"
  label var countyfips	"County of Residence"

  *state fips labels
  label define statefiplbl 1 "Alabama", add
  label define statefiplbl 2 "Alaska", add
  label define statefiplbl 4 "Arizona", add
  label define statefiplbl 5 "Arkansas", add
  label define statefiplbl 6 "California", add
  label define statefiplbl 8 "Colorado", add
  label define statefiplbl 9 "Connecticut", add
  label define statefiplbl 10 "Delaware", add
  label define statefiplbl 11 "District of Columbia", add
  label define statefiplbl 12 "Florida", add
  label define statefiplbl 13 "Georgia", add
  label define statefiplbl 15 "Hawaii", add
  label define statefiplbl 16 "Idaho", add
  label define statefiplbl 17 "Illinois", add
  label define statefiplbl 18 "Indiana", add
  label define statefiplbl 19 "Iowa", add
  label define statefiplbl 20 "Kansas", add
  label define statefiplbl 21 "Kentucky", add
  label define statefiplbl 22 "Louisiana", add
  label define statefiplbl 23 "Maine", add
  label define statefiplbl 24 "Maryland", add
  label define statefiplbl 25 "Massachusetts", add
  label define statefiplbl 26 "Michigan", add
  label define statefiplbl 27 "Minnesota", add
  label define statefiplbl 28 "Mississippi", add
  label define statefiplbl 29 "Missouri", add
  label define statefiplbl 30 "Montana", add
  label define statefiplbl 31 "Nebraska", add
  label define statefiplbl 32 "Nevada", add
  label define statefiplbl 33 "New Hampshire", add
  label define statefiplbl 34 "New Jersey", add
  label define statefiplbl 35 "New Mexico", add
  label define statefiplbl 36 "New York", add
  label define statefiplbl 37 "North Carolina", add
  label define statefiplbl 38 "North Dakota", add
  label define statefiplbl 39 "Ohio", add
  label define statefiplbl 40 "Oklahoma", add
  label define statefiplbl 41 "Oregon", add
  label define statefiplbl 42 "Pennsylvania", add
  label define statefiplbl 44 "Rhode island", add
  label define statefiplbl 45 "South Carolina", add
  label define statefiplbl 46 "South Dakota", add
  label define statefiplbl 47 "Tennessee", add
  label define statefiplbl 48 "Texas", add
  label define statefiplbl 49 "Utah", add
  label define statefiplbl 50 "Vermont", add
  label define statefiplbl 51 "Virginia", add
  label define statefiplbl 53 "Washington", add
  label define statefiplbl 54 "West Virginia", add
  label define statefiplbl 55 "Wisconsin", add
  label define statefiplbl 56 "Wyoming", add
  label values stfips statefiplbl

  compress

  sort `collapseby`dataset''

  save natality_`name`dataset'', replace

  sum

  desc
  label list

local dataset = `dataset' + 1

} /* This brace ends local dataset looping section */

log close


