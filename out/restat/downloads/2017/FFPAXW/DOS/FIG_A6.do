********************************************************************************
* Gabriel M Ahlfeldt & Daniel P. McMillen
* Tall buildings and land values in the Review of Economics and Statistics
* (C) 2017
* Stata 15.0; Windows Server 2008 R2
********************************************************************************
* THIS DO FILE GENERATES FIGURE A6

* START LOG SESSION
	log using "LOGS/FIG_A6", replace

* COMBINE DATA SETS WITH HEIGHT ELASTICITIES BY USE AND PERIOD GENERATED BY: 
* DO/FIG_8.do

	u DATA/TEMP/temp_COMMERICIAL_P1, clear
		append using DATA/TEMP/temp_RESIDENTIAL_P1
		append using DATA/TEMP/temp_OTHER_P1		
		append using DATA/TEMP/temp_COMMERICIAL_P2
		append using DATA/TEMP/temp_RESIDENTIAL_P2
		append using DATA/TEMP/temp_OTHER_P2
		append using DATA/TEMP/temp_COMMERICIAL_P3
		append using DATA/TEMP/temp_RESIDENTIAL_P3
		append using DATA/TEMP/temp_OTHER_P3
		
	
* DROP OUTLIER AND PREPARE DATA
	drop if BETA < -0.1
	drop if BETA > 0.6
	gen rBETA = round(BETA, 0.1)
		sum rBETA
		local minB = r(min)
		local maxB = r(max)
			
* PANEL 1: ELASTICITY VS. DISTANCE FROM CBD
			twoway 	///
					(scatter BETA ldist_cbd if RESIDENTIAL == 1	,  msymbol(S) mlcolor(dkgreen) mfcolor(dkgreen) msize(small)) ///
					(scatter BETA ldist_cbd if COMMER == 1	,  mfcolor(none) msymbol(T) mlcolor(orange_red)) ///
					(scatter BETA ldist_cbd if OTHER ==1,   mfcolor(none) msymbol(X)  mlcolor(blue)) ///
					,  name(PANEL1, replace) ylabel(`minB'[0.1]`maxB') xlabel(0[0.5]2.5) graphregion(color(white))  legend(order(1 "Residential" 2 "Commercial" 3 "Other") cols(3)) xtitle("Log distance from CBD") ytitle("Elasticity of height" "with respect to land price") 
* PANEL 2: ELASTICITY VS. HEIGHT	
			twoway 	///
					(scatter BETA lHEIGHT if RESIDENTIAL == 1	,  msymbol(S) mlcolor(dkgreen) mfcolor(dkgreen) msize(small)) ///
					(scatter BETA lHEIGHT if COMMER == 1	,  mfcolor(none) msymbol(T) mlcolor(orange_red)) ///
					(scatter BETA lHEIGHT if OTHER ==1,   mfcolor(none) msymbol(X)  mlcolor(blue)) ///
				,   name(PANEL2, replace) ylabel(`minB'[0.1]`maxB') xlabel(3[0.5]6) graphregion(color(white))  legend(order(1 "Residential" 2 "Commercial" 3 "Other") cols(3)) xtitle("Log height") ytitle("Elasticity of height" "with respect to land price") 
* PANEL 3: ELASTICITY VS. LAND PRICE	
			twoway 	///
					(scatter BETA nlLV if RESIDENTIAL == 1	,  msymbol(S) mlcolor(dkgreen) mfcolor(dkgreen) msize(small)) ///
					(scatter BETA nlLV if COMMER == 1	,  mfcolor(none) msymbol(T) mlcolor(orange_red)) ///
					(scatter BETA nlLV if OTHER ==1,   mfcolor(none) msymbol(X)  mlcolor(blue)) ///
				,   name(PANEL3, replace) ylabel(`minB'[0.1]`maxB') xlabel(-4[1]5) graphregion(color(white))  legend(order(1 "Residential" 2 "Commercial" 3 "Other") cols(3)) xtitle("Log land price (normalized)") ytitle("Elasticity of height" "with respect to land price") 
* PANEL 4: ELASTICITY VS. LEGAL FAR	
			twoway 	///
					(scatter BETA lFAR if RESIDENTIAL == 1	,  msymbol(S) mlcolor(dkgreen) mfcolor(dkgreen) msize(small)) ///
					(scatter BETA lFAR if COMMER == 1	,  mfcolor(none) msymbol(T) mlcolor(orange_red)) ///
					(scatter BETA lFAR if OTHER ==1,   mfcolor(none) msymbol(X)  mlcolor(blue)) ///
				,   name(PANEL4, replace) ylabel(`minB'[0.1]`maxB') xlabel(-0.5[0.5]3) graphregion(color(white))  legend(order(1 "Residential" 2 "Commercial" 3 "Other") cols(3)) xtitle("Log floor area ratio (in zoning plan)") ytitle("Elasticity of height" "with respect to land price") 

* COMBINE PANELS		
			grc1leg   PANEL1 PANEL2 PANEL3 PANEL4 , graphregion(color(white)) 
* SAVE FIGURE A6
			graph export "FIGS\FIG_A6.png", width(2400) height(1800) replace
	
* END LOG SESSION
	log close	
