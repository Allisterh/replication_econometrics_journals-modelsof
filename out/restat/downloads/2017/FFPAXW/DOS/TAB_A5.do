********************************************************************************
* Gabriel M Ahlfeldt & Daniel P. McMillen
* Tall buildings and land values in the Review of Economics and Statistics
* (C) 2017
* Stata 15.0; Windows Server 2008 R2
********************************************************************************
* THIS DO FILE GENERATES TABLE A5

* START LOG SESSION
	log using "LOGS/TAB_A5", replace

* COMBINE DATA SETS WITH HEIGHT ELASTICITIES BY USE AND PERIOD GENERATED BY: 
* DO/FIG_8.do

	u DATA/TEMP/temp_COMMERICIAL_P1, clear
		append using DATA/TEMP/temp_RESIDENTIAL_P1
		append using DATA/TEMP/temp_OTHER_P1		
		append using DATA/TEMP/temp_COMMERICIAL_P2
		append using DATA/TEMP/temp_RESIDENTIAL_P2
		append using DATA/TEMP/temp_OTHER_P2
		append using DATA/TEMP/temp_COMMERICIAL_P3
		append using DATA/TEMP/temp_RESIDENTIAL_P3
		append using DATA/TEMP/temp_OTHER_P3

	* ENCODE FAR CLASS VARIABLE
		encode FAR_class, gen(nFAR_class)
		
	* REGRESS HEIGHT ELASTICITY AGAINST COVARIATES	
	
		* [1] LAND USE, COHORT EFFECTS
			eststo: reg BETA   COMMER RESIDENTIAL i.CC if YEAR >= 1957  , robust
				estadd local Cohort_effects "Yes"
		* [2] LAND USE & GEOGRAPHIC VARIABLS, COHORT EFFECTS
			eststo: reg BETA   COMMER RESIDENTIAL i.CC ldist_cbd ldist_lm Dr if YEAR >= 1957  , robust
				estadd local Cohort_effects "Yes"
		* [3] ZONING DISTRICT TYPES, NO COHORT EFFECTS
			eststo: reg BETA  i.nFAR_class if YEAR >= 1957  , robust
				estadd local Cohort_effects "-"
		* [4] ZONING DISTRICT TYPES, COHORT EFFECTS
			eststo: reg BETA  i.nFAR_class  i.CC if YEAR >= 1957  , robust
				estadd local Cohort_effects "Yes"
		* [5] ZONING DISTRICT TYPES, LAND USE, COHORT EFFECTS
			eststo: reg BETA  i.nFAR_class COMMER RESIDENTIAL i.CC if YEAR >= 1957  , robust
				estadd local Cohort_effects "Yes"
		* [6] ZONING DISTRICT TYPES, LAND USE, GEOGRAPHIC VARIABLES, COHORT EFFECTS
			eststo: reg BETA  i.nFAR_class COMMER RESIDENTIAL i.CC ldist_cbd ldist_lm Dr if YEAR >= 1957  , robust
				estadd local Cohort_effects "Yes"

	* SAVE TABLE A5			
	esttab using "TABS/TAB_A5.rtf", replace b(3) se(3) onecell label compress r2(3) drop(*.CC  1.nFAR_class ) /// HOTEL WAREHOUSE PUBLIC CULTURE SPORTS RETAIL
	title ("Height elasticity by zoning type") modelwidth(6) nogap star(* 0.1 ** 0.05 *** 0.01) stats(Cohort_effects r2 N , fmt(%18.3g ))  ///
	addnote("Notes:	Dependent variable is the location-time specific estimate of the height elasticity from LWR illustrated in Figure 8 in the main paper. Observations include constructions after the introduction of the modern zoning plan (in 1957). FAR-0.5 throughout to FAR-PD are dummy variables denoting zoning districts with the same height lim-it. Where a number follows “FAR-“ it denotes the FAR limit in the district. FAR-PD indicates planned develop-ments, where FARs are negotiated. Baseline zoning category is missing information. Robust standard errors in parentheses.")
	eststo clear	

		
* END LOG SESSION
 log close		
