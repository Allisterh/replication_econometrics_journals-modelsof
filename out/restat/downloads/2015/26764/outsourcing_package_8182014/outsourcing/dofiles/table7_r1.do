global x "$masterpath/datafiles/"
global y "$masterpath/trade/"
global z "$masterpath/outfiles/"

#delimit;
clear;
set more off;
set matsize 800;

set mem 10000m;
global path ~;
capture log close;

/*================================================
 Program: offshore_exposure.do
 Author:  Avi Ebenstein and Shannon Phillips
 Created: March 2009
 Purpose: Assess impact of outsourcing by occupation among all workers
	   Does not include price of shipments (rpiship79)
	   so can assess economy-wide effects on all workers (N=3,481,637)
=================================================*/

use ${x}offshore_exposure.dta, replace;
collapse
p* e* l*
,
by(occ8090 year);
save ${x}temp1, replace;

use ${x}switchers.dta, clear; //changed from match_correct_mf.dta to switchers.dta
rename occ80901 occ8090;
sort occ8090 year;
merge occ8090 year using ${x}temp1;
tab _merge;
keep if _merge==3;
save ${x}temp2, replace;

use ${x}temp2, replace;
pctile offshore_exposure=p_lowincemp_effective_occ if year==2002 & manuf1==1,nq(2);
xtile offshore_cat=p_lowincemp_effective_occ if year==2002,cut(offshore_exposure);
bysort occ8090: egen globalcat=max(offshore_cat);

gen diffocc1990=occ1990_t1~=occ1990_t2;
gen diffocc1990dd=occ1990dd_t1~=occ1990dd_t2;
gen diffocc1990_1dig=occ1990_1dig_t1~=occ1990_1dig_t2;

replace ihwt1=round(ihwt1/10,0);

global controls "age1 female1 nonwhite1 union1 _Ieduc2* _Iyear2* _Istate2*";

global task_content "ehf finger dcp sts math";
global task_content " ";

gen tradable=globalcat==2;
save ${x}temp3, replace;

*** Start Regression  ***;

use ${x}temp3, clear;

egen yearcat=cut(year),at(1984(5)2010);
do $masterpath/dofiles/occ8090_fe.do;

egen occyear=group(occ8090_fe yearcat);

global controls "age1 female1 nonwhite1 union1 _Ieduc2* _Iyear2* _Istate2*";

***********************************************************;
* These two data sets are generated by table7_occ_prems.do ;  
***********************************************************;

capture drop _merge;
sort occ1990_1dig_t1;
merge occ1990_1dig_t1 using ${x}occ_prems_short;
tab _merge;

capture drop _merge;
sort occ8090;
merge occ8090 using ${x}occ_prems_long;
tab _merge;


gen premdif_1dig=0;
global occlist "2 3 4 5 6 8 10 12 13 14 15 16 17";
foreach i of global occlist{; 
replace premdif_1dig=prem_`i'-prem_`i'_sw if occ1990_1dig_t1==`i';
                          };

gen premdif_3dig=0;
do $masterpath/dofiles/occupationlist.do;
foreach i of global occupationlist{; 
replace premdif_3dig=prem_`i'_long-prem_`i'_sw_long if occ8090==`i';
                          };
drop prem_*;

reg lwagedif diffocc $controls,  cluster(occ8090);
outreg2 diffocc  using ${z}table7_r1, replace;
reg lwagedif diffocc1990_1dig $controls,  cluster(occ8090);
outreg2 diffocc1990_1dig  using ${z}table7_r1, append;
reg diffocc tradable $controls,  cluster(occ8090);
test (tradable==0);
outreg2 tradable  using ${z}table7_r1, adds(F-test, r(F)) append;
reg diffocc1990_1dig tradable $controls,  cluster(occ8090);
test (tradable==0);
outreg2 tradable  using ${z}table7_r1, adds(F-test, r(F)) append;
ivreg lwagedif (diffocc=tradable) $controls, first cluster(occ8090);
outreg2 diffocc  using ${z}table7_r1, append;
ivreg lwagedif (diffocc1990_1dig=tradable) $controls, first cluster(occ8090);
outreg2 diffocc1990_1dig  using ${z}table7_r1, append;

reg lwagedif diffocc $controls premdif_3dig,  cluster(occ8090);
outreg2 diffocc premdif_3dig  using ${z}table7_r1, append;
reg lwagedif diffocc1990_1dig $controls premdif_1dig,  cluster(occ8090);
outreg2 diffocc1990_1dig premdif_1dig using ${z}table7_r1, append;
reg diffocc tradable $controls premdif_3dig,  cluster(occ8090);
test (tradable==0);
outreg2 tradable premdif_1dig using ${z}table7_r1, adds(F-test, r(F)) append;
reg diffocc1990_1dig tradable $controls premdif_1dig,  cluster(occ8090);
test (tradable==0);
outreg2 tradable premdif_1dig using ${z}table7_r1, adds(F-test, r(F)) append;
ivreg lwagedif (diffocc=tradable) $controls premdif_3dig, first cluster(occ8090);
outreg2 diffocc premdif_3dig using ${z}table7_r1, append;
ivreg lwagedif (diffocc1990_1dig=tradable) $controls premdif_1dig, first cluster(occ8090);
outreg2 diffocc1990_1dig premdif_1dig using ${z}table7_r1, append;


exit;
